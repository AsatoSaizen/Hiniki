import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def gitCommitHash = System.getenv("VERSION_SHA") ?: providers.exec {
    commandLine("git", "rev-parse", "--verify", "--short", "HEAD")
}.standardOutput.asText.get().trim()

android {
    compileSdk 35

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "ani.himitsu"
        minSdk 21
        targetSdk 35
        versionName "1.0.3"
        versionCode = versionName.replace(".","").toInteger() * 10
        signingConfig signingConfigs.debug
        splits {
            abi {
                enable true
                reset()
                include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                universalApk true
            }
        }

        buildConfigField "String", "COMMIT", "\""+gitCommitHash+"\""
        project.archivesBaseName = "${rootProject.name}-${gitCommitHash}"
    }

    flavorDimensions += "store"
    productFlavors {
        google {
            // Google Play specific configuration
            dimension "store"
            isDefault true
            apply plugin: 'com.google.gms.google-services'
        }
    }

    buildTypes {
        matagi {
            isDefault true
            minifyEnabled Os.isFamily(Os.FAMILY_UNIX)
            shrinkResources = Os.isFamily(Os.FAMILY_UNIX)
            applicationIdSuffix ".matagi"
            versionNameSuffix "-$gitCommitHash"
            manifestPlaceholders.icon_placeholder = "@mipmap/ic_launcher_alpha"
            manifestPlaceholders.icon_placeholder_round = "@mipmap/ic_launcher_alpha_round"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            try {
                def keystoreProps = new Properties()
                keystoreProps.load(new FileInputStream(rootProject.file("keystore.properties")))
                signingConfigs {
                    document {
                        keyAlias keystoreProps['keyAlias']
                        keyPassword keystoreProps['keyPassword']
                        storeFile file(keystoreProps['storeFile'])
                        storePassword keystoreProps['storePassword']
                    }
                }
                buildTypes.debug.signingConfig signingConfigs.document
                buildTypes.matagi.signingConfig signingConfigs.document
            } catch (FileNotFoundException ignored) { }
        }
        debug {
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta02"
            manifestPlaceholders.icon_placeholder = "@mipmap/ic_launcher_beta"
            manifestPlaceholders.icon_placeholder_round = "@mipmap/ic_launcher_beta_round"
            debuggable false
        }
        release {
            manifestPlaceholders.icon_placeholder = "@mipmap/ic_launcher"
            manifestPlaceholders.icon_placeholder_round = "@mipmap/ic_launcher_round"
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-gson.pro', 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    def javaVersion = JavaVersion.VERSION_17
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
        freeCompilerArgs = ["-opt-in=kotlin.RequiresOptIn", "-Xcontext-receivers", "-Xmulti-platform"]
    }
    lint {
        abortOnError false
    }
    namespace 'ani.himitsu'
    androidResources {
        generateLocaleConfig true
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.each { output ->
            output.outputFileName = "${project.archivesBaseName}-${output.baseName}.apk"
        }
    }
}

tasks.register('torrserver') {
    try {
        def f = new File('app/libs/server.aar')
        if (!f.exists()) {
            new URL(
                    'https://github.com/RepoDevil/TorrServer/raw/kuukiyomi/server.aar'
            ).withInputStream { i -> f.withOutputStream { it << i } }
        }
    } catch (FileNotFoundException ignored) { }
    try {
        def s = new File('app/libs/server-sources.jar')
        if (!s.exists()) {
            new URL(
                    'https://github.com/RepoDevil/TorrServer/raw/kuukiyomi/server-sources.jar'
            ).withInputStream { i -> s.withOutputStream { it << i } }
        }
    } catch (FileNotFoundException ignored) { }
}

build.dependsOn tasks.torrserver

dependencies {
    implementation 'androidx.activity:activity-ktx:1.10.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    /* Core */
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    api 'androidx.appcompat:appcompat:1.7.0'
    api 'androidx.browser:browser:1.8.0'
    implementation 'com.google.androidbrowserhelper:androidbrowserhelper:2.5.0'
    api 'androidx.core:core-ktx:1.16.0'
    api 'androidx.fragment:fragment-ktx:1.8.6'
    api 'androidx.recyclerview:recyclerview:1.4.0'
    api 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.multidex:multidex:2.0.1'
    api "androidx.work:work-runtime-ktx:2.10.1"
    api 'androidx.window:window:1.3.0'
    api 'androidx.webkit:webkit:1.13.0'
    implementation "org.jetbrains.kotlin:kotlin-reflect:2.1.0"
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.github.Blatzar:NiceHttp:0.4.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.3'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation "com.anggrayudi:storage:1.5.5"

    /* User Interface */
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.github.RepoDevil:AnimatedBottomBar:66328dabd8'
    implementation 'com.github.RepoDevil:PlayLikeCurl:2e64a9e4b9'
    implementation 'com.github.8bitdream:AndroidFastScroll:3453c46352'
    implementation 'com.flaviofaria:kenburnsview:1.0.7'
    implementation 'com.alexvasilkov:gesture-views:2.8.3'
    implementation 'androidx.paging:paging-runtime-ktx:3.3.6'
    implementation 'com.github.eltos:simpledialogfragments:v3.8.3'
    implementation 'com.github.AAChartModel:AAChartCore-Kotlin:7.3.0'
    implementation 'com.github.mihonapp:subsampling-scale-image-view:64b392f85f'

    /* Ebook */
    implementation 'com.github.RepoDevil:ebook-reader:293b3ba223'

    /* Glide */
    ext.glide_version = '4.16.0'
    implementation "com.github.bumptech.glide:glide:$glide_version"
    ksp "com.github.bumptech.glide:ksp:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    implementation 'jp.wasabeef:glide-transformations:4.3.0'

    /* Firebase */
    implementation platform('com.google.firebase:firebase-bom:33.13.0')
    implementation("com.google.firebase:firebase-appcheck-playintegrity")
    implementation("com.google.firebase:firebase-auth")
    implementation("com.google.firebase:firebase-crashlytics")
    implementation("com.google.firebase:firebase-firestore")
    implementation 'com.google.firebase:firebase-storage:21.0.1'

    /* YouTube */
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:12.1.0'

    /* ExoPlayer */
    ext.exo_version = '1.6.1'
    implementation "androidx.media3:media3-exoplayer:$exo_version"
    implementation "androidx.media3:media3-ui:$exo_version"
    implementation "androidx.media3:media3-exoplayer-hls:$exo_version"
    implementation "androidx.media3:media3-exoplayer-dash:$exo_version"
    implementation "androidx.media3:media3-datasource-okhttp:$exo_version"
    implementation "androidx.media3:media3-session:$exo_version"
    ext.nextlib = '0.8.4'
    /* FFmpeg Metadata */
    implementation("com.github.anilbeesetti.nextlib:nextlib-mediainfo:$nextlib")
    /* Media3 Casting */
    implementation "androidx.media3:media3-cast:$exo_version"
    implementation "androidx.mediarouter:mediarouter:1.7.0"
    /* Media3 Preview */
    implementation 'com.github.RepoDevil.PreviewSeekBar:previewseekbar-media3:86f0cb8006'
    /* Media3 Renderers */
    implementation("com.github.anilbeesetti.nextlib:nextlib-media3ext:$nextlib")
    implementation 'io.github.peerless2012:ass-media:0.2.2'

    /* TorrServer */
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'])

    /* Markwon */
    ext.markwon_version = '4.6.2'
    implementation  "io.noties.markwon:core:$markwon_version"
    implementation  "io.noties.markwon:editor:$markwon_version"
    implementation  "io.noties.markwon:ext-strikethrough:$markwon_version"
    implementation  "io.noties.markwon:ext-tables:$markwon_version"
    implementation  "io.noties.markwon:ext-tasklist:$markwon_version"
    implementation  "io.noties.markwon:html:$markwon_version"
    implementation  "io.noties.markwon:image-glide:$markwon_version"

    /* Groupie */
    implementation "com.github.chimbori:viewgenesis:3.1.1"

    /* String Matching */
    implementation 'me.xdrop:fuzzywuzzy:1.4.0'

    /* Download */
    // implementation group: 'com.arthenica', name: 'ffmpeg-kit-full-gpl', version: '6.0-2.LTS'

    /* Biometrics */
    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha05"

    /* Aniyomi */
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'ru.beryukhov:flowreactivenetwork:1.0.4'
    implementation 'ca.gosyer:voyager-navigator:1.0.0-rc07'
    implementation 'com.squareup.logcat:logcat:0.1'
    implementation 'com.github.RepoDevil.injekt:injekt-core:bccb539b56'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.12'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.12'
    implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps'
    implementation 'com.squareup.okio:okio:3.9.1'
    implementation 'com.squareup.okhttp3:okhttp-brotli:5.0.0-alpha.12'
    implementation 'org.jsoup:jsoup:1.17.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json-okio:1.7.3'
    implementation 'com.jakewharton.rxrelay:rxrelay:1.2.0'
    implementation 'com.github.tachiyomiorg:unifile:17bec43'
    implementation 'com.github.gpanther:java-nat-sort:natural-comparator-1.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'app.cash.quickjs:quickjs-android:0.9.2'
}
